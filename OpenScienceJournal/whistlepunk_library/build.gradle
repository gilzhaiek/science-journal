apply plugin: 'com.android.library'
apply plugin: 'com.google.protobuf'
apply plugin: 'com.neenbedankt.android-apt'

apply from: 'version.gradle'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    publishNonDefault true

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 23
        versionCode buildVersionCode()
        versionName buildVersionName()
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
    }

    sourceSets {
        main {
            java {
                srcDir '../../third_party/jsyn'
                srcDir 'src/api/wireapi/java'
                srcDir 'src/api/scalarinput/java'
            }
            proto {
                srcDir 'src/main/proto'
            }
            aidl {
                srcDir 'src/api/wireapi/aidl'
                srcDir 'src/api/scalarinput/aidl'
            }
        }
        String sharedTestDir = 'src/sharedTest/java'
        test {
            java.srcDir sharedTestDir
        }
        androidTest {
            java.srcDir sharedTestDir
        }
    }
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.0.0-alpha-3'
    }
    generateProtoTasks {
        all()*.builtins {
            javanano {
                // Options added to --javanano_out
                option 'java_multiple_files=false'
                option 'ignore_services=true'
            }
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.google.protobuf.nano:protobuf-javanano:3.0.0-alpha-3.1'
    compile 'com.android.support:support-annotations:23.4.0'
    compile 'com.android.support:support-v4:23.4.0'
    compile 'com.android.support:support-v13:23.4.0'
    compile 'com.android.support:appcompat-v7:23.4.0'
    compile 'com.android.support:design:23.4.0'
    compile 'com.android.support:cardview-v7:23.4.0'
    compile 'com.android.support:recyclerview-v7:23.4.0'
    compile 'com.android.support:percent:23.4.0'
    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile 'com.google.guava:guava:19.0'
    compile 'com.google.protobuf.nano:protobuf-javanano:3.0.0-alpha-3.1'
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.4'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.4'
    testCompile 'junit:junit:4.12'
    androidTestCompile('junit:junit:4.12') {
        exclude module: 'hamcrest-core'
    }
    apt 'com.google.dagger:dagger-compiler:2.5'
    compile 'com.google.dagger:dagger:2.5'
    provided 'javax.annotation:jsr250-api:1.0'
}
